import CBPV.Typing
import CBPV.Rejoin

open Nat ValType ComType Val Com

/-*----------------------------------
  Logical equivalence (LE) of terms
----------------------------------*-/

mutual
def ğ’± (A : ValType) (v : Val) (w : Val) : Prop :=
  match A with
  | .Unit => v = unit âˆ§ w = unit
  | .Sum Aâ‚ Aâ‚‚ =>
    (âˆƒ v' w', ğ’± Aâ‚ v' w' âˆ§ v = inl v' âˆ§ w = inl w') âˆ¨
    (âˆƒ v' w', ğ’± Aâ‚‚ v' w' âˆ§ v = inr v' âˆ§ w = inr w')
  | U B => âˆƒ m n, â„° B m n âˆ§ v = thunk m âˆ§ w = thunk n

def ğ’ {Î´} (B : ComType) (m : Com Î´) (n : Com Î´) : Prop :=
  match B with
  | F A => âˆƒ v w, ğ’± A v w âˆ§ m = ret v âˆ§ n = ret w
  | Arr A B => âˆƒ m' n', (âˆ€ v w, ğ’± A v w â†’ â„° B (m'â¦ƒvâ¦„) (n'â¦ƒwâ¦„)) âˆ§
    m = lam m' âˆ§ n = lam n'
  | .Prod Bâ‚ Bâ‚‚ => âˆƒ mâ‚ mâ‚‚ nâ‚ nâ‚‚, â„° Bâ‚ mâ‚ nâ‚ âˆ§ â„° Bâ‚‚ mâ‚‚ nâ‚‚ âˆ§
    m = prod mâ‚ mâ‚‚ âˆ§ n = prod nâ‚ nâ‚‚

def â„° {Î´} (B : ComType) (m : Com Î´) (n : Com Î´) :=
  âˆƒ m' n', m â‡“â‚™ m' âˆ§ n â‡“â‚™ n' âˆ§ ğ’ B m' n'
end

notation:40 "(" v:41 "," w:41 ")" "âˆˆ" "âŸ¦" A:41 "âŸ§áµ›" => ğ’± A v w
notation:40 "(" m:41 "," n:41 ")" "âˆˆ" "âŸ¦" B:41 "âŸ§á¶œ" => ğ’ B m n
notation:40 "(" m:41 "," n:41 ")" "âˆˆ" "âŸ¦" B:41 "âŸ§áµ‰" => â„° B m n

/-* LE computations are normal and embed into LE evaluations *-/

theorem ğ’.nf {Î´ m n B} (h : (m, n) âˆˆ âŸ¦BâŸ§á¶œ) : @nf Î´ m âˆ§ @nf Î´ n := by
  match (generalizing := true) B with
  | F _ | Arr _ _ => unfold ğ’ at h; let âŸ¨_, _, _, eâ‚, eâ‚‚âŸ© := h; simp [eâ‚, eâ‚‚]
  | .Prod _ _ => unfold ğ’ at h; let âŸ¨_, _, _, _, _, _, eâ‚, eâ‚‚âŸ© := h; simp [eâ‚, eâ‚‚]

theorem ğ’â„° {Î´ A} {m n : Com Î´} (h : ğ’ A m n) : â„° A m n := by
  unfold â„°; let âŸ¨nfm, nfnâŸ© := h.nf; exact âŸ¨m, n, .refl nfm, .refl nfn, hâŸ©

/-*-------------------------------
  Convenient constructors for LE
-------------------------------*-/

theorem ğ’±.unit : ğ’± Unit unit unit := by simp [ğ’±]
theorem ğ’±.inl {v w Aâ‚ Aâ‚‚} (h : (v, w) âˆˆ âŸ¦Aâ‚âŸ§áµ›) : (inl v, inl w) âˆˆ âŸ¦Sum Aâ‚ Aâ‚‚âŸ§áµ› := by simp [ğ’±, h]
theorem ğ’±.inr {v w Aâ‚ Aâ‚‚} (h : (v, w) âˆˆ âŸ¦Aâ‚‚âŸ§áµ›) : (inr v, inr w) âˆˆ âŸ¦Sum Aâ‚ Aâ‚‚âŸ§áµ› := by simp [ğ’±, h]
theorem ğ’±.thunk {m n B} (h : (m, n) âˆˆ âŸ¦BâŸ§áµ‰) : (thunk m, thunk n) âˆˆ âŸ¦U BâŸ§áµ› := by simp [ğ’±, h]

namespace â„°

theorem ret {Î´ v w A} (h : (v, w) âˆˆ âŸ¦AâŸ§áµ›) : (@ret Î´ v, @ret Î´ w) âˆˆ âŸ¦F AâŸ§áµ‰ := by
  apply ğ’â„°; simp [ğ’, h]

theorem lam {Î´ m n A B} (h : âˆ€ v w, (v, w) âˆˆ âŸ¦AâŸ§áµ› â†’ (mâ¦ƒvâ¦„, nâ¦ƒwâ¦„) âˆˆ âŸ¦BâŸ§áµ‰) : (@lam Î´ m, @lam Î´ n) âˆˆ âŸ¦Arr A BâŸ§áµ‰ := by
  apply ğ’â„°; simp [â„°, ğ’] at *; exact h

theorem prod {Î´ mâ‚ mâ‚‚ nâ‚ nâ‚‚ Bâ‚ Bâ‚‚} (hBâ‚ : (mâ‚, nâ‚) âˆˆ âŸ¦Bâ‚âŸ§áµ‰) (hBâ‚‚ : (mâ‚‚, nâ‚‚) âˆˆ âŸ¦Bâ‚‚âŸ§áµ‰) : (@prod Î´ mâ‚ mâ‚‚, @prod Î´ nâ‚ nâ‚‚) âˆˆ âŸ¦Prod Bâ‚ Bâ‚‚âŸ§áµ‰:= by
  apply ğ’â„°; unfold ğ’; exact âŸ¨_, _, _, _, hBâ‚, hBâ‚‚, rfl, rflâŸ©

/-*-----------------------
  Inversion lemmas on LE
-----------------------*-/

theorem ret_inv {Î´ m n A} (h : (m, n) âˆˆ âŸ¦F AâŸ§áµ‰) : âˆƒ v w, m â‡’â‹† @Com.ret Î´ v âˆ§ n â‡’â‹† @Com.ret Î´ w âˆ§ (v, w) âˆˆ âŸ¦AâŸ§áµ› := by
  unfold â„° ğ’ at h
  let âŸ¨_, _, âŸ¨râ‚, _âŸ©, âŸ¨râ‚‚, _âŸ©, _, _, h, eâ‚, eâ‚‚âŸ© := h
  subst eâ‚ eâ‚‚; exact âŸ¨_, _, râ‚, râ‚‚, hâŸ©

theorem lam_inv {Î´ mâ‚ mâ‚‚ A B} (h : (mâ‚, mâ‚‚) âˆˆ âŸ¦Arr A BâŸ§áµ‰) : âˆƒ nâ‚ nâ‚‚, mâ‚ â‡’â‹† @Com.lam Î´ nâ‚ âˆ§ mâ‚‚ â‡’â‹† @Com.lam Î´ nâ‚‚ âˆ§ (âˆ€ v w, (v, w) âˆˆ âŸ¦AâŸ§áµ› â†’ (nâ‚â¦ƒvâ¦„, nâ‚‚â¦ƒwâ¦„) âˆˆ âŸ¦BâŸ§áµ‰) := by
  unfold â„° ğ’ at h
  let âŸ¨_, _, âŸ¨râ‚, _âŸ©, âŸ¨râ‚‚, _âŸ©, _, _, h, eâ‚, eâ‚‚âŸ© := h
  subst eâ‚ eâ‚‚; exact âŸ¨_, _, râ‚, râ‚‚, hâŸ©

theorem prod_inv {Î´ m n Bâ‚ Bâ‚‚} (h : (m, n) âˆˆ âŸ¦Prod Bâ‚ Bâ‚‚âŸ§áµ‰) : âˆƒ mâ‚ mâ‚‚ nâ‚ nâ‚‚, m â‡’â‹† @Com.prod Î´ mâ‚ mâ‚‚ âˆ§ n â‡’â‹† @Com.prod Î´ nâ‚ nâ‚‚ âˆ§ (mâ‚, nâ‚) âˆˆ âŸ¦Bâ‚âŸ§áµ‰ âˆ§ (mâ‚‚, nâ‚‚) âˆˆ âŸ¦Bâ‚‚âŸ§áµ‰ := by
  unfold â„° ğ’ at h
  let âŸ¨_, _, âŸ¨râ‚, _âŸ©, âŸ¨râ‚‚, _âŸ©, _, _, _, _, hâ‚, hâ‚‚, eâ‚, eâ‚‚âŸ© := h
  subst eâ‚ eâ‚‚; exact âŸ¨_, _, _, _, râ‚, râ‚‚, hâ‚, hâ‚‚âŸ©

theorem fst {Î´ m n Bâ‚ Bâ‚‚} (h : (m, n) âˆˆ âŸ¦Prod Bâ‚ Bâ‚‚âŸ§áµ‰) : âˆƒ mâ‚ mâ‚‚ nâ‚ nâ‚‚, m â‡’â‹† @Com.prod Î´ mâ‚ mâ‚‚ âˆ§ n â‡’â‹† @Com.prod Î´ nâ‚ nâ‚‚ âˆ§ (mâ‚, nâ‚) âˆˆ âŸ¦Bâ‚âŸ§áµ‰ := by
  unfold â„° ğ’ at h
  let âŸ¨_, _, âŸ¨râ‚, _âŸ©, âŸ¨râ‚‚, _âŸ©, _, _, _, _, h, _, eâ‚, eâ‚‚âŸ© := h
  subst eâ‚ eâ‚‚; exact âŸ¨_, _, _, _, râ‚, râ‚‚, hâŸ©

theorem snd {Î´ m n Bâ‚ Bâ‚‚} (h : (m, n) âˆˆ âŸ¦Prod Bâ‚ Bâ‚‚âŸ§áµ‰) : âˆƒ mâ‚ mâ‚‚ nâ‚ nâ‚‚, m â‡’â‹† @Com.prod Î´ mâ‚ mâ‚‚ âˆ§ n â‡’â‹† @Com.prod Î´ nâ‚ nâ‚‚ âˆ§ (mâ‚‚, nâ‚‚) âˆˆ âŸ¦Bâ‚‚âŸ§áµ‰ := by
  unfold â„° ğ’ at h
  let âŸ¨_, _, âŸ¨râ‚, _âŸ©, âŸ¨râ‚‚, _âŸ©, _, _, _, _, _, h, eâ‚, eâ‚‚âŸ© := h
  subst eâ‚ eâ‚‚; exact âŸ¨_, _, _, _, râ‚, râ‚‚, hâŸ©

end â„°

/-*------------
  LE is a PER
------------*-/

theorem symğ’â„° {B} (ğ’sym : âˆ€ {Î´} {m n : Com Î´}, (m, n) âˆˆ âŸ¦BâŸ§á¶œ â†’ (n, m) âˆˆ âŸ¦BâŸ§á¶œ) :
  âˆ€ {Î´} {m n : Com Î´}, (m, n) âˆˆ âŸ¦BâŸ§áµ‰ â†’ (n, m) âˆˆ âŸ¦BâŸ§áµ‰ := by
  intro _ _ _ h
  unfold â„° at *
  let âŸ¨_, _, nm, nn, hBâŸ© := h
  exact âŸ¨_, _, nn, nm, ğ’sym hBâŸ©

theorem symğ’±ğ’ :
  (âˆ€ {A v w}, (v, w) âˆˆ âŸ¦AâŸ§áµ› â†’ (w, v) âˆˆ âŸ¦AâŸ§áµ›) âˆ§
  (âˆ€ {Î´ B} {m n : Com Î´}, (m, n) âˆˆ âŸ¦BâŸ§á¶œ â†’ (n, m) âˆˆ âŸ¦BâŸ§á¶œ) := by
  refine âŸ¨Î» {A v w} h â†¦ ?val, Î» {Î´ B m n} h â†¦ ?comâŸ©
  mutual_induction A, B
  case Unit => unfold ğ’± at *; simp [h]
  case Sum ihAâ‚ ihAâ‚‚ =>
    unfold ğ’±; unfold ğ’± at h
    match h with
    | .inl âŸ¨_, _, hAâ‚, ev, ewâŸ© => refine .inl âŸ¨_, _, ihAâ‚ hAâ‚, ew, evâŸ©
    | .inr âŸ¨_, _, hAâ‚‚, ev, ewâŸ© => refine .inr âŸ¨_, _, ihAâ‚‚ hAâ‚‚, ew, evâŸ©
  case U ih =>
    unfold ğ’± at *
    let âŸ¨_, _, hA, ev, ewâŸ© := h
    exact âŸ¨_, _, symğ’â„° ih hA, ew, evâŸ©
  case F ih =>
    unfold ğ’ at *
    let âŸ¨_, _, hB, em, enâŸ© := h
    exact âŸ¨_, _, ih hB, en, emâŸ©
  case Arr ihA ihB =>
    unfold ğ’; unfold ğ’ at h
    let âŸ¨_, _, hB, em, enâŸ© := h
    exact âŸ¨_, _, Î» v w hA â†¦ symğ’â„° ihB (hB _ _ (ihA hA)), en, emâŸ©
  case Prod ihBâ‚ ihBâ‚‚ =>
    unfold ğ’; unfold ğ’ at h
    let âŸ¨_, _, _, _, hBâ‚, hBâ‚‚, em, enâŸ© := h
    exact âŸ¨_, _, _, _, symğ’â„° ihBâ‚ hBâ‚, symğ’â„° ihBâ‚‚ hBâ‚‚, en, emâŸ©

def ğ’±.sym := @symğ’±ğ’.left
def ğ’.sym := @symğ’±ğ’.right
def â„°.sym {B} := @symğ’â„° B ğ’.sym

theorem transğ’â„° {B} (ğ’trans : âˆ€ {Î´} {mâ‚ mâ‚‚ mâ‚ƒ : Com Î´}, (mâ‚, mâ‚‚) âˆˆ âŸ¦BâŸ§á¶œ â†’ (mâ‚‚, mâ‚ƒ) âˆˆ âŸ¦BâŸ§á¶œ â†’ (mâ‚, mâ‚ƒ) âˆˆ âŸ¦BâŸ§á¶œ) :
  âˆ€ {Î´} {mâ‚ mâ‚‚ mâ‚ƒ : Com Î´}, (mâ‚, mâ‚‚) âˆˆ âŸ¦BâŸ§áµ‰ â†’ (mâ‚‚, mâ‚ƒ) âˆˆ âŸ¦BâŸ§áµ‰ â†’ (mâ‚, mâ‚ƒ) âˆˆ âŸ¦BâŸ§áµ‰ := by
  intro _ _ _ _ hâ‚â‚‚ hâ‚‚â‚ƒ
  unfold â„° at *
  let âŸ¨m, m', nm, nm', hBâ‚â‚‚âŸ© := hâ‚â‚‚
  let âŸ¨n', n, nn', nn, hBâ‚‚â‚ƒâŸ© := hâ‚‚â‚ƒ
  rw [Norm.join nm' nn'] at hBâ‚â‚‚
  exact âŸ¨m, n, nm, nn, ğ’trans hBâ‚â‚‚ hBâ‚‚â‚ƒâŸ©

theorem transğ’±ğ’ :
  (âˆ€ {A vâ‚ vâ‚‚ vâ‚ƒ}, (vâ‚, vâ‚‚) âˆˆ âŸ¦AâŸ§áµ› â†’ (vâ‚‚, vâ‚ƒ) âˆˆ âŸ¦AâŸ§áµ› â†’ (vâ‚, vâ‚ƒ) âˆˆ âŸ¦AâŸ§áµ›) âˆ§
  (âˆ€ {Î´ B} {mâ‚ mâ‚‚ mâ‚ƒ : Com Î´}, (mâ‚, mâ‚‚) âˆˆ âŸ¦BâŸ§á¶œ â†’ (mâ‚‚, mâ‚ƒ) âˆˆ âŸ¦BâŸ§á¶œ â†’ (mâ‚, mâ‚ƒ) âˆˆ âŸ¦BâŸ§á¶œ) := by
  refine âŸ¨Î» {A vâ‚ vâ‚‚ vâ‚ƒ} hâ‚â‚‚ hâ‚‚â‚ƒ â†¦ ?val, Î» {Î´ B mâ‚ mâ‚‚ mâ‚ƒ} hâ‚â‚‚ hâ‚‚â‚ƒ â†¦ ?comâŸ©
  mutual_induction A, B
  case Unit =>
    unfold ğ’± at *
    let âŸ¨eâ‚, _âŸ© := hâ‚â‚‚
    let âŸ¨_, eâ‚ƒâŸ© := hâ‚‚â‚ƒ
    exact âŸ¨eâ‚, eâ‚ƒâŸ©
  case Sum ihAâ‚ ihAâ‚‚ =>
    unfold ğ’±; unfold ğ’± at hâ‚â‚‚ hâ‚‚â‚ƒ
    match hâ‚â‚‚, hâ‚‚â‚ƒ with
    | .inl âŸ¨_, _, hAâ‚â‚‚, elâ‚, elâ‚‚âŸ©, .inl âŸ¨_, _, hAâ‚‚â‚ƒ, erâ‚‚, erâ‚ƒâŸ© =>
      subst elâ‚ elâ‚‚ erâ‚ƒ; injection erâ‚‚ with e; subst e
      refine .inl âŸ¨_, _, ihAâ‚ hAâ‚â‚‚ hAâ‚‚â‚ƒ, rfl, rflâŸ©
    | .inr âŸ¨_, _, hAâ‚â‚‚, elâ‚, elâ‚‚âŸ©, .inr âŸ¨_, _, hAâ‚‚â‚ƒ, erâ‚‚, erâ‚ƒâŸ© =>
      subst elâ‚ elâ‚‚ erâ‚ƒ; injection erâ‚‚ with e; subst e
      refine .inr âŸ¨_, _, ihAâ‚‚ hAâ‚â‚‚ hAâ‚‚â‚ƒ, rfl, rflâŸ©
    | .inl âŸ¨_, _, _, _, erâŸ©, .inr âŸ¨_, _, _, _, _âŸ© => subst er; contradiction
    | .inr âŸ¨_, _, _, _, erâŸ©, .inl âŸ¨_, _, _, _, _âŸ© => subst er; contradiction
  case U ih =>
    unfold ğ’± at *
    let âŸ¨_, _, hBâ‚â‚‚, elâ‚, elâ‚‚âŸ© := hâ‚â‚‚
    let âŸ¨_, _, hBâ‚‚â‚ƒ, erâ‚‚, erâ‚ƒâŸ© := hâ‚‚â‚ƒ
    subst elâ‚ elâ‚‚ erâ‚ƒ; injection erâ‚‚ with e; subst e
    exact âŸ¨_, _, transğ’â„° ih hBâ‚â‚‚ hBâ‚‚â‚ƒ, rfl, rflâŸ©
  case F ih =>
    unfold ğ’ at *
    let âŸ¨_, _, hAâ‚â‚‚, elâ‚, elâ‚‚âŸ© := hâ‚â‚‚
    let âŸ¨_, _, hAâ‚‚â‚ƒ, erâ‚‚, erâ‚ƒâŸ© := hâ‚‚â‚ƒ
    subst elâ‚ elâ‚‚ erâ‚ƒ; injection erâ‚‚ with _ e; subst e
    exact âŸ¨_, _, ih hAâ‚â‚‚ hAâ‚‚â‚ƒ, rfl, rflâŸ©
  case Arr ihA ihB =>
    unfold ğ’; unfold ğ’ at hâ‚â‚‚ hâ‚‚â‚ƒ
    let âŸ¨m, m', hBâ‚â‚‚, elâ‚, elâ‚‚âŸ© := hâ‚â‚‚
    let âŸ¨_, n, hBâ‚‚â‚ƒ, erâ‚‚, erâ‚ƒâŸ© := hâ‚‚â‚ƒ
    subst elâ‚ elâ‚‚ erâ‚ƒ; injection erâ‚‚ with _ e; subst e
    refine âŸ¨_, _, Î» v w hA â†¦ ?_, rfl, rflâŸ©
    apply transğ’â„° ihB (hBâ‚â‚‚ v w hA) (hBâ‚‚â‚ƒ w w (ihA hA.sym hA))
  case Prod ihBâ‚ ihBâ‚‚ =>
    unfold ğ’; unfold ğ’ at hâ‚â‚‚ hâ‚‚â‚ƒ
    let âŸ¨_, _, _, _, hAâ‚â‚, hAâ‚â‚‚, elâ‚, elâ‚‚âŸ© := hâ‚â‚‚
    let âŸ¨_, _, _, _, hAâ‚‚â‚, hAâ‚‚â‚‚, erâ‚, erâ‚‚âŸ© := hâ‚‚â‚ƒ
    subst elâ‚ elâ‚‚ erâ‚‚; injection erâ‚ with _ eâ‚ eâ‚‚; subst eâ‚ eâ‚‚
    refine âŸ¨_, _, _, _, transğ’â„° ihBâ‚ hAâ‚â‚ hAâ‚‚â‚, transğ’â„° ihBâ‚‚ hAâ‚â‚‚ hAâ‚‚â‚‚, rfl, rflâŸ©

def ğ’±.trans := @transğ’±ğ’.left
def ğ’.trans := @transğ’±ğ’.right
def â„°.trans {B} := @transğ’â„° B ğ’.trans

/-*-----------------------------
  LE evals are backward closed
-----------------------------*-/

namespace â„°

theorem bwds {Î´ B} {m m' n n' : Com Î´} (rm : m â‡’â‹† m') (rn : n â‡’â‹† n') (h : (m', n') âˆˆ âŸ¦BâŸ§áµ‰) : (m, n) âˆˆ âŸ¦BâŸ§áµ‰ := by
  unfold â„° at *
  let âŸ¨m'', n'', nm', nn', hâŸ© := h
  exact âŸ¨m'', n'', nm'.bwds rm, nn'.bwds rn, hâŸ©

theorem bwdsRejoin {Î´ B Ï† Ïˆ} {m m' n n' : Com 0} (rm : m â‡’â‹† m') (rn : n â‡’â‹† n') (h : (m', n') âˆˆ âŸ¦BâŸ§áµ‰) :
  (rejoin (weakenJCom Î´ m) Ï†, rejoin (weakenJCom Î´ n) Ïˆ) âˆˆ âŸ¦BâŸ§áµ‰ := by
  unfold â„° at *
  let âŸ¨m'', n'', nm', nn', hâŸ© := h
  refine âŸ¨m'', n'', nm'.bwdsRejoin rm, nn'.bwdsRejoin rn, hâŸ©

theorem bwdsRejoin0 {Î´ B Ï† Ïˆ} {m m' n n' : Com 0} (rm : m â‡’â‹† weakenJCom 0 m') (rn : n â‡’â‹† weakenJCom 0 n') (h : (m', n') âˆˆ âŸ¦BâŸ§áµ‰) :
  (rejoin (weakenJCom Î´ m) Ï†, rejoin (weakenJCom Î´ n) Ïˆ) âˆˆ âŸ¦BâŸ§áµ‰ := by
  rw [weakenJCom0] at rm rn; exact bwdsRejoin rm rn h

theorem bwd {Î´ B} {m m' n n' : Com Î´} (rm : m â‡’ m') (rn : n â‡’ n') :
  (m', n') âˆˆ âŸ¦BâŸ§áµ‰ â†’ (m, n) âˆˆ âŸ¦BâŸ§áµ‰ := â„°.bwds (.once rm) (.once rn)
theorem bwdRejoin {Î´ B Ï† Ïˆ} {m m' n n' : Com 0} (rm : m â‡’ m') (rn : n â‡’ n') :
  (m', n') âˆˆ âŸ¦BâŸ§áµ‰ â†’ (rejoin (weakenJCom Î´ m) Ï†, rejoin (weakenJCom Î´ n) Ïˆ) âˆˆ âŸ¦BâŸ§áµ‰ :=
  â„°.bwdsRejoin (.once rm) (.once rn)
theorem bwdRejoin0 {Î´ B Ï† Ïˆ} {m m' n n' : Com 0} (rm : m â‡’ weakenJCom 0 m') (rn : n â‡’ weakenJCom 0 n') :
  (m', n') âˆˆ âŸ¦BâŸ§áµ‰ â†’ (rejoin (weakenJCom Î´ m) Ï†, rejoin (weakenJCom Î´ n) Ïˆ) âˆˆ âŸ¦BâŸ§áµ‰ :=
  â„°.bwdsRejoin0 (.once rm) (.once rn)

end â„°

/-*---------------------
  Semantic equivalence
---------------------*-/

/-* Semantic equivalence of contexts as a PER *-/

def semCtxt Î“ (Ïƒ Ï„ : Nat â†’ Val) := âˆ€ {x A}, Î“ âˆ‹ x âˆ¶ A â†’ (Ïƒ x, Ï„ x) âˆˆ âŸ¦ A âŸ§áµ›
notation:40 Î“:41 "âŠ¨" Ïƒ:41 "~" Ï„:41 => semCtxt Î“ Ïƒ Ï„

namespace semCtxt

theorem nil : â¬ âŠ¨ var ~ var := Î» mem â†¦ by cases mem
theorem cons {Î“ Ïƒ Ï„ v w A} (h : (v, w) âˆˆ âŸ¦ A âŸ§áµ›) (hÏƒÏ„ : Î“ âŠ¨ Ïƒ ~ Ï„) : Î“ âˆ· A âŠ¨ v +: Ïƒ ~ w +: Ï„
  | _, _, .here => h
  | _, _, .there mem => hÏƒÏ„ mem

theorem sym {Î“ Ïƒ Ï„} (h : Î“ âŠ¨ Ïƒ ~ Ï„) : Î“ âŠ¨ Ï„ ~ Ïƒ := ğ’±.sym âˆ˜ h
theorem trans {Î“ Ï Ïƒ Ï„} (hÏÏƒ : Î“ âŠ¨ Ï ~ Ïƒ) (hÏƒÏ„ : Î“ âŠ¨ Ïƒ ~ Ï„) : Î“ âŠ¨ Ï ~ Ï„ := Î» mem â†¦ ğ’±.trans (hÏÏƒ mem) (hÏƒÏ„ mem)
theorem rename {Î¾ Ïƒ Ï„} {Î“ Î” : Ctxt} (hÎ¾ : Î“ âŠ¢ Î¾ âˆ¶ Î”) (h : Î“ âŠ¨ Ïƒ ~ Ï„) : Î” âŠ¨ Ïƒ âˆ˜ Î¾ ~ Ï„ âˆ˜ Î¾ := h âˆ˜ hÎ¾ _ _

end semCtxt

/-* Semantic equivalence of join point contexts as a PER *-/

section
set_option hygiene false
local notation:40 Î”:41 "âŠ¨" Ï†:41 "~" Ïˆ:41 => semDtxt Î” Ï† Ïˆ
inductive semDtxt : âˆ€ {Î´}, Dtxt Î´ â†’ J Î´ â†’ J Î´ â†’ Prop where
  | nil : â¬ âŠ¨ .nil ~ .nil
  | cons {Î´} {Î” : Dtxt Î´} {Ï† Ïˆ m n A B} : Î” âŠ¨ Ï† ~ Ïˆ â†’
    (âˆ€ {v w}, (v, w) âˆˆ âŸ¦ A âŸ§áµ› â†’ (rejoin (mâ¦ƒvâ¦„) Ï†, rejoin (nâ¦ƒwâ¦„) Ïˆ) âˆˆ âŸ¦ B âŸ§áµ‰) â†’
    Î” âˆ· A â†— B âŠ¨ .cons m Ï† ~ .cons n Ïˆ
end
notation:40 Î”:41 "âŠ¨" Ï†:41 "~" Ïˆ:41 => semDtxt Î” Ï† Ïˆ

theorem semDtxt.sym {Î´} {Î” : Dtxt Î´} {Ï† Ïˆ} (h : Î” âŠ¨ Ï† ~ Ïˆ) : Î” âŠ¨ Ïˆ ~ Ï† := by
  induction h
  case nil => exact .nil
  case cons h ih => exact .cons ih (Î» hvw â†¦  .sym (h hvw.sym))

theorem semDtxt.trans {Î´} {Î” : Dtxt Î´} {Ï†â‚ Ï†â‚‚ Ï†â‚ƒ} (hâ‚â‚‚ : Î” âŠ¨ Ï†â‚ ~ Ï†â‚‚) (hâ‚‚â‚ƒ : Î” âŠ¨ Ï†â‚‚ ~ Ï†â‚ƒ) : Î” âŠ¨ Ï†â‚ ~ Ï†â‚ƒ := by
  induction hâ‚â‚‚
  case nil => exact hâ‚‚â‚ƒ
  case cons hâ‚â‚‚ ih =>
    cases hâ‚‚â‚ƒ; case cons hÏ† hâ‚‚â‚ƒ =>
    refine .cons (ih hÏ†) (Î» hvw â†¦  ?_)
    exact â„°.trans (hâ‚â‚‚ hvw) (hâ‚‚â‚ƒ (ğ’±.trans hvw.sym hvw))

/-* Semantic equivalence of values and computations *-/

@[simp] def semVal (Î“ : Ctxt) (v w : Val) A := âˆ€ {Ïƒ Ï„}, Î“ âŠ¨ Ïƒ ~ Ï„ â†’ (vâ¦ƒÏƒâ¦„, wâ¦ƒÏ„â¦„) âˆˆ âŸ¦ A âŸ§áµ›
@[simp] def semCom (Î“ : Ctxt) {Î´} (Î” : Dtxt Î´) (m n : Com Î´) B := âˆ€ {Ïƒ Ï„}, Î“ âŠ¨ Ïƒ ~ Ï„ â†’ âˆ€ {Ï† Ïˆ}, Î” âŠ¨ Ï† ~ Ïˆ â†’ (rejoin (mâ¦ƒÏƒâ¦„) Ï†, rejoin (nâ¦ƒÏ„â¦„) Ïˆ) âˆˆ âŸ¦ B âŸ§áµ‰
notation:40 Î“:41 "âŠ¨" v:41 "~" w:41 "âˆ¶" A:41 => semVal Î“ v w A
notation:40 Î“:41 "âˆ£" Î”:41 "âŠ¨" m:41 "~" n:41 "âˆ¶" B:41 => semCom Î“ Î” m n B

/-* Semantic equivalence is a PER *-/

theorem semVal.sym {Î“ v w} {A : ValType} (h : Î“ âŠ¨ v ~ w âˆ¶ A) : Î“ âŠ¨ w ~ v âˆ¶ A :=
  Î» hÏƒÏ„ â†¦ (h hÏƒÏ„.sym).sym
theorem semCom.sym {Î“ Î´ Î”} {m n : Com Î´} {B : ComType} (h : Î“ âˆ£ Î” âŠ¨ m ~ n âˆ¶ B) : Î“ âˆ£ Î” âŠ¨ n ~ m âˆ¶ B :=
  Î» hÏƒÏ„ _ _ hÏ†Ïˆ â†¦ (h hÏƒÏ„.sym hÏ†Ïˆ.sym).sym

theorem semVal.trans {Î“ vâ‚ vâ‚‚ vâ‚ƒ} {A : ValType} (hâ‚â‚‚ : Î“ âŠ¨ vâ‚ ~ vâ‚‚ âˆ¶ A) (hâ‚‚â‚ƒ : Î“ âŠ¨ vâ‚‚ ~ vâ‚ƒ âˆ¶ A) : Î“ âŠ¨ vâ‚ ~ vâ‚ƒ âˆ¶ A :=
  Î» hÏƒÏ„ â†¦ by refine ğ’±.trans (hâ‚â‚‚ hÏƒÏ„) (hâ‚‚â‚ƒ (semCtxt.trans hÏƒÏ„.sym hÏƒÏ„))
theorem semCom.trans {Î“ Î´ Î”} {mâ‚ mâ‚‚ mâ‚ƒ : Com Î´} {B : ComType} (hâ‚â‚‚ : Î“ âˆ£ Î” âŠ¨ mâ‚ ~ mâ‚‚ âˆ¶ B) (hâ‚‚â‚ƒ : Î“ âˆ£ Î” âŠ¨ mâ‚‚ ~ mâ‚ƒ âˆ¶ B) : Î“ âˆ£ Î” âŠ¨ mâ‚ ~ mâ‚ƒ âˆ¶ B :=
  Î» hÏƒÏ„ _ _ hÏ†Ïˆ â†¦ by refine â„°.trans (hâ‚â‚‚ hÏƒÏ„ hÏ†Ïˆ) (hâ‚‚â‚ƒ (semCtxt.trans hÏƒÏ„.sym hÏƒÏ„) (semDtxt.trans hÏ†Ïˆ.sym hÏ†Ïˆ))

/-*---------------------------------------------
  Fundamental theorem of soundness
  of syntactic typing wrt semantic equivalence
---------------------------------------------*-/

theorem semCom.join {Î“ Î´} {Î” : Dtxt Î´} {A B mâ‚ mâ‚‚ nâ‚ nâ‚‚} (hm : Î“ âˆ· A âˆ£ Î” âŠ¨ mâ‚ ~ mâ‚‚ âˆ¶ B) (hn : Î“ âˆ£ Î” âˆ· A â†— B âŠ¨ nâ‚ ~ nâ‚‚ âˆ¶ B) :
  Î“ âˆ£ Î” âŠ¨ join mâ‚ nâ‚ ~ join mâ‚‚ nâ‚‚ âˆ¶ B := by
  intro Ïƒ Ï„ hÏƒÏ„ Ï† Ïˆ hÏ†Ïˆ; simp; rw [â† rejoin.eq_2, â† rejoin.eq_2]
  refine hn hÏƒÏ„ (.cons hÏ†Ïˆ (Î» {v w} hvw â†¦ ?ihm))
  rw [substUnion, substUnion]
  exact hm (semCtxt.cons hvw hÏƒÏ„) hÏ†Ïˆ

theorem semCom.jump {Î“ : Ctxt} {Î´} {Î” : Dtxt Î´} {Ï† Ïˆ j A B} (mem : Î” âˆ‹ j âˆ¶ A â†— B) (h : Î” âŠ¨ Ï† ~ Ïˆ) :
  âˆ€ {Ïƒ Ï„ v w}, Î“ âŠ¨ Ïƒ ~ Ï„ â†’ (v, w) âˆˆ âŸ¦ A âŸ§áµ› â†’
  (rejoin (jump j v) Ï†, rejoin (jump j w) Ïˆ) âˆˆ âŸ¦ B âŸ§áµ‰ := by
  induction h generalizing A B
  case nil => cases mem
  case cons Ï† Ïˆ m n _ _ _ h _ =>
    cases mem
    case here =>
      intro Ïƒ Ï„ v w hÏƒÏ„ hvw; simp
      exact â„°.bwd (.rejoin .Î³) (.rejoin .Î³) (h hvw)
    case there ih _ mem =>
      intro Ïƒ Ï„ v w hÏƒÏ„ hvw; simp
      exact .bwd (.rejoin .join't) (.rejoin .join't) (ih mem hÏƒÏ„ hvw)

theorem soundness {Î“} :
  (âˆ€ (v : Val) A, Î“ âŠ¢ v âˆ¶ A â†’ Î“ âŠ¨ v ~ v âˆ¶ A) âˆ§
  (âˆ€ {Î´ Î”} (m : Com Î´) B, Î“ âˆ£ Î” âŠ¢ m âˆ¶ B â†’ Î“ âˆ£ Î” âŠ¨ m ~ m âˆ¶ B) := by
  refine âŸ¨Î» v A h â†¦ ?val, Î» m B h â†¦ ?comâŸ©
  mutual_induction h, h
  all_goals intro Ïƒ Ï„ hÏƒÏ„
  case var mem => exact hÏƒÏ„ mem
  case unit => exact ğ’±.unit
  case inl ih => exact ğ’±.inl (ih hÏƒÏ„)
  case inr ih => exact ğ’±.inr (ih hÏƒÏ„)
  case thunk ih => exact ğ’±.thunk (ih hÏƒÏ„ .nil)
  all_goals intro Ï† Ïˆ hÏ†Ïˆ
  case force ih =>
    simp [ğ’±] at ih
    let âŸ¨_, _, h, em, enâŸ© := ih hÏƒÏ„
    simp [em, en]; exact â„°.bwdRejoin0 .Ï€ .Ï€ h
  case lam m _ _ _ ih =>
    refine â„°.bwdsRejoin0 (m' := (lam m)â¦ƒÏƒâ¦„) (n' := (lam m)â¦ƒÏ„â¦„) .refl .refl (â„°.lam (Î» v w hA â†¦ ?_))
    rw [substUnion, substUnion]
    exact ih (semCtxt.cons hA hÏƒÏ„) .nil
  case app ihm ihv =>
    let âŸ¨_ ,_, râ‚, râ‚‚, hABâŸ© := (ihm hÏƒÏ„ .nil).lam_inv
    let hB := hAB _ _ (ihv hÏƒÏ„)
    exact â„°.bwdsRejoin0 (.trans' (Evals.app râ‚) (.once .Î²)) (.trans' (Evals.app râ‚‚) (.once .Î²)) hB
  case ret ih => exact â„°.bwdsRejoin .refl .refl (â„°.ret (ih hÏƒÏ„))
  case letin ihm ihn =>
    let âŸ¨v, w, râ‚, râ‚‚, hAâŸ© := (ihm hÏƒÏ„ .nil).ret_inv
    refine â„°.bwds ?_ ?_ (ihn (semCtxt.cons hA hÏƒÏ„) hÏ†Ïˆ)
    all_goals rw [â† substUnion]
    . exact Evals.rejoin (.trans' (Evals.letin râ‚) (.once .Î¶))
    . exact Evals.rejoin (.trans' (Evals.letin râ‚‚) (.once .Î¶))
  case case ihv ihm ihn =>
    simp [ğ’±] at ihv
    match ihv hÏƒÏ„ with
    | .inl âŸ¨v, w, hAâ‚, ev, ewâŸ© =>
      simp [ev, ew]
      refine â„°.bwd ?_ ?_ (ihm (semCtxt.cons hAâ‚ hÏƒÏ„) hÏ†Ïˆ)
      all_goals rw [â† substUnion]; exact .rejoin .Î¹l
    | .inr âŸ¨v, w, hAâ‚‚, ev, ewâŸ© =>
      simp [ev, ew]
      refine â„°.bwd ?_ ?_ (ihn (semCtxt.cons hAâ‚‚ hÏƒÏ„) hÏ†Ïˆ)
      all_goals rw [â† substUnion]; exact .rejoin .Î¹r
  case prod ihm ihn =>
    exact â„°.bwdsRejoin .refl .refl (â„°.prod (ihm hÏƒÏ„ .nil) (ihn hÏƒÏ„ .nil))
  case fst ih =>
    let âŸ¨_, _, _, _, râ‚, râ‚‚, hBâ‚âŸ© := (ih hÏƒÏ„ .nil).fst
    exact â„°.bwdsRejoin0 (.trans' (Evals.fst râ‚) (.once .Ï€1)) (.trans' (Evals.fst râ‚‚) (.once .Ï€1)) hBâ‚
  case snd ih =>
    let âŸ¨_, _, _, _, râ‚, râ‚‚, hBâ‚‚âŸ© := (ih hÏƒÏ„ .nil).snd
    exact â„°.bwdsRejoin0 (.trans' (Evals.snd râ‚) (.once .Ï€2)) (.trans' (Evals.snd râ‚‚) (.once .Ï€2)) hBâ‚‚
  case join m n _ _ _ _ ihm ihn => exact semCom.join ihm ihn hÏƒÏ„ hÏ†Ïˆ
  case jump mem _ ihv => exact semCom.jump mem hÏ†Ïˆ hÏƒÏ„ (ihv hÏƒÏ„)

def soundVal {Î“ v} {A : ValType} : Î“ âŠ¢ v âˆ¶ A â†’ Î“ âŠ¨ v ~ v âˆ¶ A := soundness.left v A
def soundCom {Î“ Î´} {Î” : Dtxt Î´} {m} {B : ComType} : Î“ âˆ£ Î” âŠ¢ m âˆ¶ B â†’ Î“ âˆ£ Î” âŠ¨ m ~ m âˆ¶ B := soundness.right m B

theorem safety {m B} (h : â¬ âˆ£ â¬ âŠ¢ m âˆ¶ B) : âˆƒ n, m â‡“â‚™ n := by
  have hB := soundCom h semCtxt.nil semDtxt.nil
  rw [substComId] at hB; simp at hB; unfold â„° at hB
  let âŸ¨_, _, nfm, _, _âŸ© := hB; exact âŸ¨_, nfmâŸ©
